#+TITLE: DotEmacs
#+AUTHOR: Mathieu Marques

A. *[[#a-bootstrap][Bootstrap]]* |
B. *[[#b-theme][Theme]]* |
C. *[[#c-languages][Languages]]* |
C. *[[#d-features][Features]]*

* A. Bootstrap

1. *[[#1-use-better-defaults][Use better defaults]]*
2. *[[#2-validate-emacs-version][Validate Emacs' version]]*
3. *[[#3-benchmark-startup-time][Benchmark startup time]]*
4. *[[#4-load-customel][Load .custom.el]]*
5. *[[#5-load-secretel][Load .secret.el]]*
6. *[[#6-initialize-melpa][Initialize MELPA]]*

** 1. Use better defaults

Here is what I consider better defaults for my own experience.

#+BEGIN_SRC emacs-lisp
(setq-default
 ad-redefinition-action 'accept                  ; Silence warnings for redefinition
 auto-window-vscroll t                           ; Normalize vertical scroll offset
 confirm-kill-emacs 'yes-or-no-p                 ; Confirm before exiting Emacs
 cursor-in-non-selected-windows t                ; Hide the cursor in inactive windows
 delete-by-moving-to-trash t                     ; Delete files to trash
 display-time-default-load-average nil           ; Don't display load average
 display-time-format "%H:%M"                     ; Format the time string
 fill-column 85                                  ; Set width for automatic line breaking
 gc-cons-threshold most-positive-fixnum          ; Increase garbage collector treshold
 help-window-select t                            ; Focus new help windows when opened
 indent-tabs-mode nil                            ; Stop using tabs to indent
 inhibit-startup-screen t                        ; Disable start-up screen
 initial-scratch-message ""                      ; Empty the initial *scratch* buffer
 mouse-yank-at-point t                           ; Yank at point rather than cursor
 require-final-newline 'visit                    ; Add a newline at EOF on visit
 scroll-step 1                                   ; Fix the scroll line step
 show-trailing-whitespace nil                    ; Display trailing whitespaces
 split-height-threshold nil                      ; Disable vertical window splitting
 split-width-threshold nil                       ; Disable horizontal window splitting
 tab-width 4                                     ; Set width for tabs
 uniquify-buffer-name-style 'forward             ; Uniquify buffer names
 window-combination-resize t                     ; Resize windows proportionally
 x-select-enable-clipboard t)                    ; Merge system's and Emacs' clipboard
#+END_SRC

Some UI elements are rather invasive and oppose to the no-mouse philosophy.

#+BEGIN_SRC emacs-lisp
(when window-system
  (blink-cursor-mode 0)                          ; Disable the cursor blinking
  (scroll-bar-mode 0)                            ; Disable the scroll bar
  (tool-bar-mode 0)                              ; Disable the tool bar
  (tooltip-mode 0))                              ; Disable the tooltips
(column-number-mode 0)                           ; Hide the column number
(display-battery-mode 0)                         ; Hide the battery level
(display-time-mode 1)                            ; Hide the time representation
(fringe-mode '(10 . 10))                         ; Show vertical fringes
(global-hl-line-mode 1)                          ; Hightlight current line
(global-subword-mode 1)                          ; Iterate through CamelCase words
(line-number-mode 1)                             ; Show the line number
(menu-bar-mode 0)                                ; Disable the menu bar
(mouse-avoidance-mode 'animate)                  ; Move pointer to avoid collision with point
(set-frame-parameter nil 'fullscreen 'fullboth)  ; Enable pseudo fullscreen
#+END_SRC

Enable some more goodies.

#+BEGIN_SRC emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)                    ; Replace yes/no prompts with y/n
(put 'downcase-region 'disabled nil)             ; Enable downcase-region
(put 'upcase-region 'disabled nil)               ; Enable upcase-region
#+END_SRC

Reset garbage collector treshold after initialization is finished and
=garbage-collect= on focus-out. Emacs /should/ feel snappier, I do however
experience long garbage collects when returning to the Emacs frame.

#+BEGIN_SRC emacs-lisp
(add-hook 'after-init-hook (lambda () (setq gc-cons-threshold 800000)))
(add-hook 'focus-out-hook 'garbage-collect)
#+END_SRC

I want an easy way to change the appearance of things and be consistent about
it. Those variables are used throughout the whole configuration. You could always
customize these in =~/.emacs.d/.secret.el= file.

#+BEGIN_SRC emacs-lisp
(defvar me/font-family            "Monaco"  "The font to use.")
(defvar me/font-size-default      120       "The font size to use for default text.")
(defvar me/font-size-header       140       "The font size to use for headers.")
(defvar me/font-size-mode-line    120       "The font size to use for the mode line.")
#+END_SRC

** 2. Validate Emacs' version

Most of the configuration is written and tested on a daily basis with Emacs 24.

- *TODO*: Check how it actually behave on a <24 Emacs.

#+BEGIN_SRC emacs-lisp
(when (version<= emacs-version "24")
  (unless (yes-or-no-p "Your Emacs is getting old. Kitten may be killed, continue? ")
    (kill-emacs)))
#+END_SRC

** 3. Benchmark startup time

Currently 1.7 seconds start-up time. But this is cheating: this snippet of code
should live as early as in =init.el=.

- *TODO*: Actually use it.
- *FIXME*: Find a way to dynamically look for =benchmark-init.el=.

#+BEGIN_SRC emacs-lisp
(let ((benchmark-init.el
       (expand-file-name
        "elpa/benchmark-init-20150905.238/benchmark-init.el" user-emacs-directory)))
  (when (file-exists-p benchmark-init.el)
    (load benchmark-init.el)))
#+END_SRC

** 4. Load =.custom.el=

One is able to use the customization interface that is bundled within Emacs. It is
meant to help people who are not familiar with Emacs Lisp in the configuration of Emacs
itself. By default, changes in the customization will be automatically detected and
appended at the end of the configuration file, =init.el=.

Since that in my case, the actual configuration file is new one, crafted by
=org-mode=, adding code at the end of =init.el= might mess things up. The following
tells Emacs to add extra code in another file that would be then loaded, if existing
that is.

#+BEGIN_SRC emacs-lisp
(setq-default custom-file (expand-file-name ".custom.el" user-emacs-directory))
(when (file-exists-p custom-file)
  (load custom-file))
#+END_SRC

** 5. Load =.secret.el=

I load =~/.emacs.d/.secret.el= to keep sensible things out of version control.
For instance, you could set your identity by customizing both =user-full-name= and
=user-mail-address= in this file.

#+BEGIN_SRC emacs-lisp
(let ((secret.el (expand-file-name ".secret.el" user-emacs-directory)))
  (when (file-exists-p secret.el)
    (load secret.el)))
#+END_SRC

** 6. Initialize MELPA

Dependency checks and [[https://melpa.org][MELPA]] initialization, sugar-wrapped.

- *FIXME*: Do I really need to ensure =delight= here?
- *FIXME*: It sometimes does an extra =package-refresh-contents=.

#+BEGIN_SRC emacs-lisp
(when (>= emacs-major-version 24)
  (setq-default
   load-prefer-newer t
   package-enable-at-startup nil)
  (package-initialize)
  (add-to-list 'package-archives '("melpa" . "http://melpa.org/packages/") t)
  (unless (package-installed-p 'delight)
    (package-refresh-contents)
    (package-install 'delight))
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package)))
#+END_SRC

In order to have unified package declarations in my Emacs files, I use
[[https://github.com/jwiegley/use-package][use-package]]. It allows for isolation and clarity.

#+BEGIN_QUOTE
The =use-package= macro allows you to isolate package configuration in your =.emacs=
file in a way that is both performance-oriented and, well, tidy. I created it because
I have over 80 packages that I use in Emacs, and things were getting difficult to
manage. Yet with this utility my total load time is around 2 seconds, with no loss of
functionality!

[[https://github.com/jwiegley/use-package][John Wiegley]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(eval-when-compile
  (require 'use-package)
  (setq-default
   use-package-always-defer t
   use-package-always-ensure t))
#+END_SRC

* B. Theme

1. *[[#1-set-a-color-palette][Set a color palette]]*
2. *[[#2-configure-zenburn][Configure Zenburn]]*

** 1. Set a color palette

I like to have an accessible copy of my active theme's color codes.
That way, I can fix other packages' colors while still using the same color codes and
keep a sane consistency everywhere.

I consider this method safe because I am using the all-famous [[https://github.com/bbatsov/zenburn-emacs][Zenburn Emacs port]]
palette. It is /very/ unlikely that any of its tones will ever change.

#+BEGIN_SRC emacs-lisp
(defconst zenburn/bg+3      "#6F6F6F"  "Zenburn palette: #6F6F6F.")
(defconst zenburn/bg+2      "#5F5F5F"  "Zenburn palette: #5F5F5F.")
(defconst zenburn/bg+1      "#4F4F4F"  "Zenburn palette: #4F4F4F.")
(defconst zenburn/bg+0      "#494949"  "Zenburn palette: #494949.")
(defconst zenburn/bg        "#3F3F3F"  "Zenburn palette: #3F3F3F.")
(defconst zenburn/bg-0      "#383838"  "Zenburn palette: #383838.")
(defconst zenburn/bg-1      "#2B2B2B"  "Zenburn palette: #2B2B2B.")
(defconst zenburn/bg-2      "#000000"  "Zenburn palette: #000000.")
(defconst zenburn/blue+1    "#94BFF3"  "Zenburn palette: #94BFF3.")
(defconst zenburn/blue      "#8CD0D3"  "Zenburn palette: #8CD0D3.")
(defconst zenburn/blue-1    "#7CB8BB"  "Zenburn palette: #7CB8BB.")
(defconst zenburn/blue-2    "#6CA0A3"  "Zenburn palette: #6CA0A3.")
(defconst zenburn/blue-3    "#5C888B"  "Zenburn palette: #5C888B.")
(defconst zenburn/blue-4    "#4C7073"  "Zenburn palette: #4C7073.")
(defconst zenburn/blue-5    "#366060"  "Zenburn palette: #366060.")
(defconst zenburn/cyan      "#93E0E3"  "Zenburn palette: #93E0E3.")
(defconst zenburn/fg+1      "#FFFFEF"  "Zenburn palette: #FFFFEF.")
(defconst zenburn/fg        "#DCDCCC"  "Zenburn palette: #DCDCCC.")
(defconst zenburn/fg-1      "#656555"  "Zenburn palette: #656555.")
(defconst zenburn/green+4   "#BFEBBF"  "Zenburn palette: #BFEBBF.")
(defconst zenburn/green+3   "#AFD8AF"  "Zenburn palette: #AFD8AF.")
(defconst zenburn/green+2   "#9FC59F"  "Zenburn palette: #9FC59F.")
(defconst zenburn/green+1   "#8FB28F"  "Zenburn palette: #8FB28F.")
(defconst zenburn/green     "#7F9F7F"  "Zenburn palette: #7F9F7F.")
(defconst zenburn/green-1   "#5F7F5F"  "Zenburn palette: #5F7F5F.")
(defconst zenburn/magenta   "#DC8CC3"  "Zenburn palette: #DC8CC3.")
(defconst zenburn/orange    "#DFAF8F"  "Zenburn palette: #DFAF8F.")
(defconst zenburn/red+1     "#DCA3A3"  "Zenburn palette: #DCA3A3.")
(defconst zenburn/red       "#CC9393"  "Zenburn palette: #CC9393.")
(defconst zenburn/red-1     "#BC8383"  "Zenburn palette: #BC8383.")
(defconst zenburn/red-2     "#AC7373"  "Zenburn palette: #AC7373.")
(defconst zenburn/red-3     "#9C6363"  "Zenburn palette: #9C6363.")
(defconst zenburn/red-4     "#8C5353"  "Zenburn palette: #8C5353.")
(defconst zenburn/yellow    "#F0DFAF"  "Zenburn palette: #F0DFAF.")
(defconst zenburn/yellow-1  "#E0CF9F"  "Zenburn palette: #E0CF9F.")
(defconst zenburn/yellow-2  "#D0BF8F"  "Zenburn palette: #D0BF8F.")
#+END_SRC

** 2. Configure Zenburn

Load =zenburn-theme= and fix some high-level faces to match my personal preferences.

#+BEGIN_SRC emacs-lisp
(use-package zenburn-theme
  :init (load-theme 'zenburn t)
  :config
  (set-face-attribute 'default nil :height me/font-size-default)
  (set-face-attribute 'font-lock-comment-delimiter-face nil
                      :foreground zenburn/bg+3 :italic t)
  (set-face-attribute 'font-lock-comment-face nil :italic t)
  (set-face-attribute 'font-lock-doc-face nil :italic t)
  (set-face-attribute 'font-lock-function-name-face nil :foreground zenburn/blue)
  (set-face-attribute 'fringe nil :background zenburn/bg :foreground zenburn/bg+3)
  (set-face-attribute 'header-line nil
                      :box `(:line-width 1 :color ,zenburn/bg-1)
                      :height me/font-size-header)
  (set-face-attribute 'help-argument-name nil :foreground zenburn/orange)
  (set-face-attribute 'hl-line nil :background zenburn/bg+1)
  (set-face-attribute 'region nil :foreground zenburn/green)
  (set-face-attribute 'vertical-border nil :foreground zenburn/bg-1)
  (when (member me/font-family (font-family-list))
    (set-face-attribute 'default nil :font me/font-family)))
#+END_SRC

* C. Languages

1. *[[#1-css][CSS]]*
2. *[[#2-docker][Docker]]*
3. *[[#3-html][HTML]]*
4. *[[#4-javascript][JavaScript]]*
5. *[[#5-lisp][Lisp]]*
6. *[[#6-markdown][Markdown]]*
7. *[[#7-org][Org]]*
8. *[[#8-python][Python]]*
9. *[[#9-yaml][YAML]]*

#+BEGIN_QUOTE
As pedantic as I like to be, some packages have a =use-package= clause for nothing but
the customization of its lighter.
#+END_QUOTE

- *TODO*: Add a list of all custom shortcuts.
- *TODO*: Replace all old defadvice's.

** 1. CSS

#+BEGIN_SRC emacs-lisp
(use-package css-mode
  :delight css-mode "CSS"
  :config (setq-default css-indent-offset 2))

(use-package scss-mode
  :delight scss-mode "SCSS"
  :mode ("\\.css\\'" "\\.sass\\'" "\\.scss\\'")
  :init
  (add-hook 'scss-mode-hook
            (lambda ()
              (setq-local comment-end "")
              (setq-local comment-start "//")))
  :config (setq-default scss-compile-at-save nil))
#+END_SRC

** 2. Docker

#+BEGIN_SRC emacs-lisp
(use-package dockerfile-mode
  :delight dockerfile-mode "Dockerfile"
  :mode "Dockerfile\\'")
#+END_SRC

** 3. HTML

HTML mode is defined in =sgml-mode.el=.

#+BEGIN_SRC emacs-lisp
(use-package sgml-mode
  :ensure nil
  :delight html-mode "HTML"
  :config (setq-default sgml-basic-offset 2))
#+END_SRC

** 4. JavaScript

Tern is a code-analysis engine for JavaScript. I use it to check syntax and grammar
in my JavaScript code. With an auto-complete frontend, it can also provide candidates
for variables and properties.

- *FIXME*: Indent level is broken
           (https://github.com/joshwnj/json-mode/issues/32).

#+BEGIN_SRC emacs-lisp
(use-package js
  :delight js-mode "JavaScript"
  :config (setq-default js-indent-level 2))

(use-package json-mode
  :delight json-mode "JSON"
  :mode "\\.json\\'"
  :config
  (add-hook 'json-mode-hook (lambda () (setq-local js-indent-level 2))))

(use-package tern
  :config (add-hook 'js-mode-hook 'tern-mode))
#+END_SRC

** 5. Lisp

#+BEGIN_SRC emacs-lisp
(use-package lisp-mode
  :ensure nil
  :delight lisp-mode "Lisp"
  :config
  (delight
   '((emacs-lisp-mode "Emacs Lisp")
     (lisp-interaction-mode "Lisp Interaction"))))
#+END_SRC

** 6. Markdown

#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :delight markdown-mode "Markdown"
  :mode
  ("INSTALL\\'"
   "CONTRIBUTORS\\'"
   "LICENSE\\'"
   "README\\'"
   "\\.markdown\\'"
   "\\.md\\'")
  :init (add-hook 'markdown-mode-hook 'turn-on-auto-fill)
  :config
  (unbind-key "M-<down>" markdown-mode-map)
  (unbind-key "M-<up>" markdown-mode-map)
  (setq-default markdown-asymmetric-header t))
#+END_SRC

** 7. Org

This very file is organized with =org-mode=. I am definitely not a power user of Org,
but I'm getting there. :-)

#+BEGIN_QUOTE
Org mode is for keeping notes, maintaining TODO lists, planning projects, and
authoring documents with a fast and effective plain-text system.

[[http://orgmode.org/][Carsten Dominik]]
#+END_QUOTE

- *FIXME*: Fix =hl-line= in source blocks.
- *TODO*: Enable whitespace-mode in edit mode.

#+BEGIN_SRC emacs-lisp
(use-package org
  :delight org-mode "Org"

  :bind (:map org-mode-map ("<M-return>" . org-insert-heading-after-current))

  :init (add-hook 'org-mode-hook 'turn-on-auto-fill)

  :config
  (setq-default
   org-edit-src-content-indentation 0
   org-src-fontify-natively t
   org-src-window-setup 'current-window
   org-support-shift-select 'always
   org-startup-folded nil
   org-startup-truncated nil)
  (set-face-attribute 'org-block-background nil :background zenburn/bg+0)
  (set-face-attribute 'org-block-begin-line nil :background 'unspecified)
  (set-face-attribute 'org-block-end-line nil :background 'unspecified)
  (unbind-key "C-a" org-mode-map)
  (unbind-key "C-e" org-mode-map)
  (unbind-key "<C-return>" org-mode-map)
  (unbind-key "<C-S-down>" org-mode-map)
  (unbind-key "<C-S-up>" org-mode-map))
#+END_SRC

** 8. Python

#+BEGIN_SRC emacs-lisp
(use-package python
  :delight python-mode "Python")

(use-package pip-requirements
  :delight pip-requirements-mode "PyPA Requirements"
  :config
  (add-hook 'pip-requirements-mode-hook
            (lambda () (setq-local completion-ignore-case t))))
#+END_SRC

** 9. YAML

#+BEGIN_SRC emacs-lisp
(use-package yaml-mode
  :delight yaml-mode "YAML"
  :mode "\\.yml\\'")
#+END_SRC

* D. Features

1. *[[#1-auto-completion][Auto-Completion]]*
2. *[[#2-comments][Comments]]*
3. *[[#3-customization-menus][Customization Menus]]*
4. *[[#4-diff][Diff]]*
5. *[[#5-dired][Dired]]*
6. *[[#6-expand][Expand]]*
7. *[[#7-git][Git]]*
8. *[[#8-helm][Helm]]*
9. *[[#9-helm-plugins][Helm Plugins]]*
10. *[[#10-hydra][Hydra]]*
11. *[[#11-linters][Linters]]*
12. *[[#12-mode-line][Mode-line]]*
13. *[[#13-navigation][Navigation]]*
14. *[[#14-os-x][OS X]]*
15. *[[#15-parentheses][Parentheses]]*
16. *[[#16-point][Point]]*
17. *[[#17-projectile][Projectile]]*
18. *[[#18-quality-of-life][Quality of Life]]*
19. *[[#19-slack][Slack]]*
20. *[[#20-whitespaces][Whitespaces]]*
21. *[[#21-web-browsing][Web Browsing]]*
22. *[[#22-windows][Windows]]*

** 1. Auto-Completion

Auto-completion at point. Display a small pop-in containing the candidates.

#+BEGIN_QUOTE
Company is a text completion framework for Emacs. The name stands for "complete
anything". It uses pluggable back-ends and front-ends to retrieve and display
completion candidates.

[[http://company-mode.github.io/][Dmitry Gutov]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package company
  :init (global-company-mode)
  :config
  (setq-default
   company-idle-delay .2
   company-minimum-prefix-length 1
   company-tooltip-align-annotations t))

(use-package company-tern
  :init (add-to-list 'company-backends 'company-tern)
  :config
  (setq-default
   company-tern-meta-as-single-line t
   company-tern-property-marker " *"))
#+END_SRC

** 2. Comments

#+BEGIN_SRC emacs-lisp
(use-package newcomment
  :ensure nil
  :bind ("<M-return>" . comment-indent-new-line)
  :config
  (setq-default
   comment-auto-fill-only-comments t
   comment-multi-line t))
#+END_SRC

** 3. Customization Menus

This merely changes face attributes. It also /Zenburn/ customization buffers a little
more.

- *TODO*: Open a pull request over [[https://github.com/bbatsov/zenburn-emacs][zenburn-emacs]].

#+BEGIN_SRC emacs-lisp
(use-package cus-edit
  :ensure nil
  :config
  (set-face-attribute 'custom-group-tag nil
                      :foreground zenburn/yellow :height me/font-size-header)
  (set-face-attribute 'custom-state nil :foreground zenburn/green+4)
  (set-face-attribute 'custom-variable-tag nil :foreground zenburn/blue)
  (when (member me/font-family (font-family-list))
    (set-face-attribute 'custom-group-tag nil :font me/font-family)))
#+END_SRC

** 4. Diff

Ediff is a visual interface to Unix =diff=.

#+BEGIN_SRC emacs-lisp
(use-package ediff-util
  :ensure nil
  :preface
  (defun me/setup-ediff-map ()
    (ediff-setup-keymap)
    (define-key ediff-mode-map (kbd "<down>") #'ediff-next-difference)
    (define-key ediff-mode-map (kbd "<up>") #'ediff-previous-difference))
  :init (add-hook 'ediff-mode-hook #'me/setup-ediff-map))

(use-package ediff-wind
  :ensure nil
  :config
  (setq-default
   ediff-split-window-function #'split-window-horizontally
   ediff-window-setup-function #'ediff-setup-windows-plain))
#+END_SRC

** 5. Dired

Configure Dired buffers. Amongst many other things, Emacs is also a file explorer.

- *TODO*: Use the new syntax for advices.

#+BEGIN_SRC emacs-lisp
(use-package dired
  :ensure nil
  :delight dired-mode "Dired"

  :config

  (defadvice dired-readin (after dired-after-updating-hook first () activate)
    "Sort dired listings with directories first before adding marks."
    (save-excursion
      (let (buffer-read-only)
        (forward-line 2) ;; beyond dir. header
        (sort-regexp-fields t "^.*$" "[ ]*." (point) (point-max)))
      (set-buffer-modified-p nil)))

  (setq-default
   dired-auto-revert-buffer t
   dired-listing-switches "-alh"
   dired-ls-F-marks-symlinks nil
   dired-recursive-copies 'always))

(use-package dired-x
  :ensure nil
  :preface
  (defun me/dired-revert-after-command (command &optional output error)
    (revert-buffer))
  :config
  (advice-add 'dired-smart-shell-command :after #'me/dired-revert-after-command))
#+END_SRC

** 6. Expand

HippieExpand manages expansions a la [[http://emmet.io/][Emmet]]. So I've gathered all features that look
anywhere close to this behavior for it to handle under the same bind, that is
=<C-return>=. Basically it's an expand DWIM.

#+BEGIN_SRC emacs-lisp
(use-package emmet-mode
  :init
  (add-hook 'css-mode-hook 'emmet-mode)
  (add-hook 'sgml-mode-hook 'emmet-mode)
  :config
  (setq-default emmet-move-cursor-between-quote t)
  (unbind-key "<C-return>" emmet-mode-keymap)
  (unbind-key "C-M-<left>" emmet-mode-keymap)
  (unbind-key "C-M-<right>" emmet-mode-keymap))

(use-package hippie-exp
  :ensure nil
  :preface
  (defun me/emmet-try-expand-line (args)
    "Try `emmet-expand-line' if `emmet-mode' is active. Else, does nothing."
     (interactive "P")
     (when emmet-mode (emmet-expand-line args)))
  :bind ("<C-return>" . hippie-expand)
  :config
  (setq-default
   hippie-expand-try-functions-list '(yas-hippie-try-expand me/emmet-try-expand-line)
   hippie-expand-verbose nil))

(use-package yasnippet
  :init
  (add-hook 'emacs-lisp-mode-hook 'yas-minor-mode)
  (add-hook 'js-mode-hook 'yas-minor-mode)
  (add-hook 'org-mode-hook 'yas-minor-mode)
  (add-hook 'sgml-mode-hook 'yas-minor-mode)
  :config
  (setq-default yas-snippet-dirs '("~/.emacs.d/snippets"))
  (yas-reload-all)
  (unbind-key "TAB" yas-minor-mode-map)
  (unbind-key "<tab>" yas-minor-mode-map))
#+END_SRC

** 7. Git

Magit provide Git facilities directly from within Emacs.

#+BEGIN_QUOTE
Magit is an interface to the version control system [[https://git-scm.com/][Git]], implemented as an [[https://www.gnu.org/software/emacs][Emacs]]
package. Magit aspires to be a complete Git porcelain. While we cannot (yet) claim
that Magit wraps and improves upon each and every Git command, it is complete enough
to allow even experienced Git users to perform almost all of their daily version
control tasks directly from within Emacs. While many fine Git clients exist, only
Magit and Git itself deserve to be called porcelains. [[https://magit.vc/about.html][(more)]]

[[https://github.com/magit/magit][Jonas Bernoulli]]
#+END_QUOTE

- *TODO*: Make a hydra.

#+BEGIN_SRC emacs-lisp
(use-package magit

  :bind
  (("C-c g b" . magit-blame)
   ("C-c g l" . magit-log)
   ("C-c g p" . magit-pull)
   ("C-c g s" . magit-status))

  :config

  (defun me/magit-display-buffer-function (buffer)
    "Render some magit modes in the currently selected buffer."
    (display-buffer
     buffer
     (cond ((and (derived-mode-p 'magit-mode)
                 (eq (with-current-buffer buffer major-mode)
                     'magit-status-mode))
            nil)
           ((memq (with-current-buffer buffer major-mode)
                  '(magit-process-mode
                    magit-revision-mode
                    magit-diff-mode
                    magit-stash-mode))
            nil)
           (t
            '(display-buffer-same-window)))))

  ;; Use better defaults
  (setq-default
   magit-display-buffer-function 'me/magit-display-buffer-function
   magit-diff-highlight-hunk-body nil
   magit-popup-display-buffer-action '((display-buffer-same-window))
   magit-refs-show-commit-count (quote all)
   magit-section-show-child-count t
   magit-set-upstream-on-push 'askifnotset)

  ;; Customize lighters
  (delight
   '((magit-diff-mode "Magit Diff")
     (magit-log-mode "Magit Log")
     (magit-popup-mode "Magit Popup")
     (magit-status-mode "Magit Status")))

  ;; Customize faces
  (set-face-attribute 'magit-diff-added nil
                      :background zenburn/bg+0 :foreground zenburn/green+3)
  (set-face-attribute 'magit-diff-context nil :background zenburn/bg+0)
  (set-face-attribute 'magit-diff-file-heading-highlight nil :background 'unspecified)
  (set-face-attribute 'magit-diff-hunk-heading nil :background zenburn/bg+2)
  (set-face-attribute 'magit-diff-hunk-heading-highlight nil :background zenburn/bg+2)
  (set-face-attribute 'magit-diff-removed nil
                      :background zenburn/bg+0 :foreground zenburn/red)
  (set-face-attribute 'magit-popup-heading nil :height me/font-size-header)
  (set-face-attribute 'magit-section-heading nil :height me/font-size-header)
  (set-face-attribute 'magit-section-highlight nil :background 'unspecified))

(use-package gitattributes-mode :delight gitattributes-mode "Git Attributes")
(use-package gitconfig-mode :delight gitconfig-mode "Git Config")
(use-package gitignore-mode :delight gitignore-mode "Git Ignore")
#+END_SRC

** 8. Helm

Helm is a beast. Although heavily, it replaces =ido-mode= in many ways.

#+BEGIN_QUOTE
=Helm= is an Emacs framework for incremental completions and narrowing selections. It
helps to rapidly complete file names, buffer names, or any other Emacs interactions
requiring selecting an item from a list of possible choices.

Helm is a fork of =anything.el=, which was originally written by Tamas Patrovic and
can be considered to be its successor. =Helm= cleans the legacy code that is leaner,
modular, and unchained from constraints of backward compatibility.

[[https://github.com/emacs-helm/helm][Bozhidar Batsov]]
#+END_QUOTE

- *TODO*: Make helm-list-faces-display.
- *TODO*: Fix the mode-line for Helm buffers.

#+BEGIN_SRC emacs-lisp
(use-package helm
  :defer 1
  :bind*
  (:map helm--minor-mode-map
        ("<left>" . backward-char)
        ("<right>" . forward-char))
  :config
  (setq-default
   helm-always-two-windows t
   helm-display-header-line nil
   helm-split-window-default-side 'left)
  (helm-mode 1)
  (set-face-attribute 'helm-match nil :foreground zenburn/green+2 :weight 'normal)
  (set-face-attribute 'helm-prefarg nil :foreground zenburn/red)
  (set-face-attribute 'helm-source-header nil
                      :box nil :background 'unspecified :height me/font-size-header))
#+END_SRC

Helm sub-modules can be customized separately. Many basic Emacs commands have their
Helm equivalents.

#+BEGIN_SRC emacs-lisp
(use-package helm-buffers
  :ensure nil
  :after helm
  :config
  (setq-default helm-buffers-fuzzy-matching t)
  (set-face-attribute 'helm-buffer-directory nil
                      :background 'unspecified :foreground zenburn/blue)
  (set-face-attribute 'helm-buffer-size nil :foreground zenburn/bg+3))

(use-package helm-color
  :ensure nil
  :after helm)

(use-package helm-command
  :ensure nil
  :after helm
  :bind ([remap execute-extended-command] . helm-M-x)
  :config
  (setq-default helm-M-x-fuzzy-match t)
  (set-face-attribute 'helm-M-x-key nil :foreground zenburn/orange :underline nil))

(use-package helm-files
  :ensure nil
  :after helm
  :bind (:map helm-find-files-map ("C-s" . helm-ff-run-grep-ag))
  :config
  (set-face-attribute 'helm-ff-dotted-directory nil :background 'unspecified))

(use-package helm-grep
  :ensure nil
  :after helm
  :config (set-face-attribute 'helm-grep-lineno nil :foreground zenburn/yellow-2))

(use-package helm-misc
  :ensure nil
  :after helm
  :bind ([remap switch-to-buffer] . helm-buffers-list))

(use-package helm-mode
  :ensure nil
  :after helm
  :config
  (setq-default
   helm-completion-in-region-fuzzy-match t
   helm-mode-fuzzy-match t))

(use-package helm-net
  :ensure nil
  :after helm
  :config (setq-default helm-net-prefer-curl t))

(use-package helm-regexp
  :ensure nil
  :after helm
  :config (set-face-attribute 'helm-moccur-buffer nil :foreground zenburn/blue))
#+END_SRC

** 9. Helm Plugins

Fourth-party packages for Helm.

- *NOTE*: Does not respect =helm-split-window-default-side=
          (https://github.com/ShingoFukuyama/helm-css-scss/issues/7).
- *TODO*: Fix Zenburn palette
          (https://github.com/bbatsov/zenburn-emacs/issues/220).

#+BEGIN_SRC emacs-lisp
(use-package helm-ag
  :after helm
  :config (setq-default helm-ag-show-status-function nil))

(use-package helm-css-scss
  :after helm
  :config (setq-default helm-css-scss-split-direction 'split-window-horizontally))

(use-package helm-descbinds
  :after helm
  :bind ([remap describe-key] . helm-descbinds)
  :config (setq-default helm-descbinds-window-style 'split-window))

(use-package helm-describe-modes
  :after helm
  :bind ([remap describe-mode] . helm-describe-modes))

(use-package helm-flycheck
  :after helm)

(use-package helm-projectile
  :after helm
  :bind
  (:map projectile-command-map
        ("b" . helm-projectile-switch-to-buffer)
        ("d" . helm-projectile-find-dir)
        ("f" . helm-projectile-find-file)
        ("p" . helm-projectile-switch-project)
        ("s s" . helm-projectile-ag))
  :config (helm-projectile-toggle 1))
#+END_SRC

** 10. Hydra

Hydra allows me to group binds together. It also shows a list of all implemented
commands in the eho area.

#+BEGIN_QUOTE
Once you summon the Hydra through the prefixed binding (the body + any one head), all
heads can be called in succession with only a short extension.

The Hydra is vanquished once Hercules, any binding that isn't the Hydra's head,
arrives. Note that Hercules, besides vanquishing the Hydra, will still serve his
original purpose, calling his proper command. This makes the Hydra very seamless,
it's like a minor mode that disables itself auto-magically.

[[https://github.com/abo-abo/hydra][Oleh Krehel]]
#+END_QUOTE

- *TODO*: Prefix own functions.

#+BEGIN_SRC emacs-lisp
(use-package hydra
  :bind
  ("C-c h" . hydra-helm/body)
  ("C-c p" . hydra-projectile/body)
  ("C-c z" . hydra-zoom/body))
#+END_SRC

Group Helm commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-helm (:color blue :hint nil)
  "
  ^
  ^Meta^              ^Browse^            ^Do^
 ─^────^───────────  ─^──────^─────────  ─^──^─────────────
  [_q_] quit          [_c_] colors        [_f_] flycheck
  [_r_] resume        [_g_] google        [_s_] css-scss
  ^^                  [_i_] imenu
"
  ("q" nil)
  ("c" helm-colors)
  ("f" helm-flycheck)
  ("g" helm-google-suggest)
  ("i" helm-imenu)
  ("r" helm-resume)
  ("s" helm-css-scss))
#+END_SRC

Group Projectile commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-projectile (:color blue :hint nil)
  "
  ^
  ^Meta^              ^Buffers^           ^Find^              ^Search^
 ─^────^───────────  ─^────^───────────  ─^──────^─────────  ─^──^─────────────
  [_q_] quit          [_b_] list all      [_d_] directory     [_r_] replace
  [_i_] reset cache   [_k_] kill all      [_D_] root          [_s_] ag
  ^^                  [_S_] save all      [_f_] file
  ^^                  ^^                  [_p_] project
"
  ("q" nil)
  ("b" helm-projectile-switch-to-buffer)
  ("d" helm-projectile-find-dir)
  ("D" projectile-dired)
  ("f" helm-projectile-find-file)
  ("i" projectile-invalidate-cache :color red)
  ("k" projectile-kill-buffers)
  ("p" helm-projectile-switch-project)
  ("r" projectile-replace)
  ("s" helm-projectile-ag)
  ("S" projectile-save-project-buffers :color red))
#+END_SRC

Change text size.

- *TODO*: Prettify.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-zoom () "Zoom"
  ("-" text-scale-decrease "out")
  ("=" text-scale-increase "in")
  ("0" (text-scale-increase 0) "reset"))
#+END_SRC

** 11. Linters

Flycheck lints warnings and errors directly within buffers. It can check a lot of
different syntaxes, as long as you make sure that Emacs has access to the binaries
(see [[./README.md][README.md]]).

#+BEGIN_SRC emacs-lisp
(use-package flycheck

  :bind
  (("C-c e l" . list-flycheck-errors)
   ("C-c e p" . flycheck-previous-error)
   ("C-c e n" . flycheck-next-error))

  :init
  (add-hook 'emacs-lisp-mode-hook 'flycheck-mode)
  (add-hook 'js-mode-hook 'flycheck-mode)
  (add-hook 'python-mode-hook 'flycheck-mode)
  (add-hook 'scss-mode-hook 'flycheck-mode)

  :config
  (setq-default
   flycheck-check-syntax-automatically '(save mode-enabled)
   flycheck-disabled-checkers '(emacs-lisp-checkdoc)
   flycheck-display-errors-delay .3
   flycheck-flake8rc "~/.flake8rc"
   flycheck-jshintrc "~/.jshintrc"
   flycheck-pylintrc "~/.pylintrc")
  (set-face-attribute 'flycheck-error nil :underline zenburn/red-1)
  (set-face-attribute 'flycheck-info nil :underline zenburn/blue+1)
  (set-face-attribute 'flycheck-warning nil :underline zenburn/orange)
  (set-face-attribute 'flycheck-fringe-error nil :foreground zenburn/red-1)
  (set-face-attribute 'flycheck-fringe-info nil :foreground zenburn/blue+1)
  (set-face-attribute 'flycheck-fringe-warning nil :foreground zenburn/orange))
#+END_SRC

** 12. Mode-line

Delight allows you to change modes --- both major and minor --- lighters. They are
the descriptive strings than you see appear within the =mode-line=.

To make it work with =spaceline= (which uses =powerline= internally), I need to allow
it to use the newly changed strings.

- *TODO*: Check whether =diminish= rename major mode lighters.
- *TODO*: Use the new syntax for advices.

#+BEGIN_SRC emacs-lisp
(use-package delight
  :config
  (defadvice powerline-major-mode (around delight-powerline-major-mode activate)
    (let ((inhibit-mode-name-delight nil)) ad-do-it))
  (defadvice powerline-minor-modes (around delight-powerline-minor-modes activate)
    (let ((inhibit-mode-name-delight nil)) ad-do-it)))
#+END_SRC

Spaceline, is a mode-line configuration framework. Like what =powerline= doesm but at
a shallower level. It's still very customizable nonetheless.

#+BEGIN_QUOTE
This is the package that provides [[http://spacemacs.org/][Spacemacs]] with its famous mode-line theme. It has
been extracted as an independent package for general fun and profit.

[[https://github.com/TheBB/spaceline][Eivind Fonn]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package spaceline :demand t)
(use-package spaceline-config
  :ensure nil
  :after spaceline

  :config

  ;; Configure the mode-line
  (setq-default
   mode-line-format '("%e" (:eval (spaceline-ml-main)))
   powerline-default-separator 'wave
   powerline-height 20
   spaceline-highlight-face-func 'spaceline-highlight-face-modified
   spaceline-flycheck-bullet "• %s"
   spaceline-separator-dir-left '(left . left)
   spaceline-separator-dir-right '(right . right))
  (spaceline-helm-mode)

  ;; Build the segments
  (spaceline-define-segment me/version-control
    (when vc-mode
      (substring vc-mode (+ 2 (length (symbol-name (vc-backend buffer-file-name)))))))
  (spaceline-define-segment me/helm-follow
    (when (and (bound-and-true-p helm-alive-p)
               spaceline--helm-current-source
               (eq 1 (cdr (assq 'follow spaceline--helm-current-source))))
      (propertize "" 'face 'success)))

  ;; Build the mode-lines
  (spaceline-install
   `((major-mode :face highlight-face)
     ((remote-host buffer-id line) :separator ":"))
   `((selection-info)
     ((flycheck-error flycheck-warning flycheck-info) :when active)
     ((projectile-root me/version-control) :separator "  ")
     (global :face highlight-face)))
  (spaceline-install
   'helm
   '((helm-buffer-id :face spaceline-read-only)
     helm-number
     (me/helm-follow :fallback "")
     helm-prefix-argument)
   '((helm-help :face spaceline-read-only)))

  ;; Customize the mode-line
  (set-face-attribute 'mode-line nil
                      :box `(:line-width 1 :color ,zenburn/bg-2)
                      :foreground zenburn/bg+3
                      :height me/font-size-mode-line)
  (set-face-attribute 'mode-line-inactive nil
                      :box `(:line-width 1 :color ,zenburn/bg-2)
                      :foreground zenburn/bg+3
                      :height me/font-size-mode-line)
  (set-face-attribute 'powerline-active2 nil :background zenburn/bg+2)
  (set-face-attribute 'powerline-inactive2 nil :background zenburn/bg+2)
  (set-face-attribute 'spaceline-flycheck-error nil :foreground zenburn/red)
  (set-face-attribute 'spaceline-flycheck-info nil :foreground zenburn/blue)
  (set-face-attribute 'spaceline-flycheck-warning nil :foreground zenburn/orange)
  (set-face-attribute 'spaceline-modified nil
                      :background zenburn/red :foreground zenburn/red-4)
  (set-face-attribute 'spaceline-read-only nil
                      :background zenburn/blue+1 :foreground zenburn/blue-5)
  (set-face-attribute 'spaceline-unmodified nil
                      :background zenburn/green :foreground zenburn/green+4))
#+END_SRC

** 13. Navigation

I disagree with Emacs' definition of paragraphs so I redefined the way it should jump
from one block to another.

#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "<C-down>") 'me/goto-next-block)
(global-set-key (kbd "<C-up>") 'me/goto-previous-block)

(defun me/goto-next-block ()
  "Jump to next paragraph."
  (interactive "^")
  (skip-chars-forward "\n")
  (unless (search-forward-regexp "\n[[:blank:]]*\n" nil t)
    (goto-char (point-max)))
  (skip-chars-forward "\n"))

(defun me/goto-previous-block ()
  "Jump to previous paragraph."
  (interactive "^")
  (skip-chars-backward "\n")
  (unless (search-backward-regexp "\n[[:blank:]]*\n" nil t)
    (goto-char (point-min)))
  (skip-chars-forward "\n"))
#+END_SRC

Bind commands to move around windows.

#+BEGIN_SRC emacs-lisp
(use-package windmove
  :ensure nil
  :bind
  (("C-M-<left>". windmove-left)
   ("C-M-<right>". windmove-right)
   ("C-M-<up>". windmove-up)
   ("C-M-<down>". windmove-down)))
#+END_SRC

** 14. OS X

Augment Emacs experience for OS X users.

#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (setq-default
   exec-path (append exec-path '("/usr/local/bin"))  ; Add path to Homebrew binaries
   ns-command-modifier 'meta                         ; Map the Meta key to the `cmd' key
   ns-option-modifier nil))                          ; Disable the `alt' key
#+END_SRC

** 15. Parentheses

Highlight parenthese-like delimiters in a rainbow fashion. It ease the reading when
dealing with mismatched parentheses.

#+BEGIN_SRC emacs-lisp
(use-package rainbow-delimiters
  :init
  (add-hook 'prog-mode-hook #'rainbow-delimiters-mode)
  (add-hook 'scss-mode-hook #'rainbow-delimiters-mode))
#+END_SRC

I am still looking for the perfect parenthesis management setup as of today... No
package seem to please my person.

- *TODO*: Find a better parenthese management package.

#+BEGIN_SRC emacs-lisp
(use-package smartparens
  :bind
  (("M-<backspace>" . sp-unwrap-sexp)
   ("M-<left>" . sp-forward-barf-sexp)
   ("M-<right>" . sp-forward-slurp-sexp)
   ("M-S-<left>" . sp-backward-slurp-sexp)
   ("M-S-<right>" . sp-backward-barf-sexp))
  :init (require 'smartparens-config)
  :config
  (smartparens-global-mode 1)
  (sp-pair "{{" "}}")
  (sp-pair "[[" "]]"))
#+END_SRC

** 16. Point

Increase region by semantic units. It tries to be smart about it and adapt to the
structure of the current major mode.

#+BEGIN_SRC emacs-lisp
(use-package expand-region
  :bind ("C-=" . er/expand-region)
  :init (pending-delete-mode t))
#+END_SRC

Enable multiple cursors at once. Some witchcraft at work here.

#+BEGIN_SRC emacs-lisp
(use-package multiple-cursors
  :bind
  (("C-S-<mouse-1>" . mc/add-cursor-on-click)
   ("C-S-c C-S-a" . mc/vertical-align-with-space)
   ("C-S-c C-S-c" . mc/edit-lines)
   ("C-S-c C-S-l" . mc/insert-letters)
   ("C-S-c C-S-n" . mc/insert-numbers)
   ("C-'" . mc-hide-unmatched-lines-mode))
  :init
  (setq-default
   mc/edit-lines-empty-lines 'ignore
   mc/insert-numbers-default 1
   mc/list-file (expand-file-name ".multiple-cursors.el" user-emacs-directory)))
#+END_SRC

Enable new custom binds when region is active. I've also added a few helpers to use
with =selected=.

- *TODO*: Make a hydra.

#+BEGIN_SRC emacs-lisp
(use-package selected
  :defines selected-keymap
  :bind
  (:map selected-keymap
        ("C-c C-c" . me/eval-region-and-kill-mark)
        ("<M-left>" . me/indent-rigidly-left-and-keep-mark)
        ("<M-right>" . me/indent-rigidly-right-and-keep-mark)
        ("<" . mc/mark-previous-like-this)
        (">" . mc/mark-next-like-this)
        ("C-b b" . me/browse-url-and-kill-mark)
        ("C-c c" . capitalize-region)
        ("C-c l" . downcase-region)
        ("C-c u" . upcase-region)
        ("C-f f" . fill-region)
        ("C-g" . selected-off)
        ("C-s r" . reverse-region)
        ("C-s s" . sort-lines)
        ("C-s w" . me/sort-words))
  :init (selected-global-mode))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun me/eval-region-and-kill-mark (beg end)
  "Execute the region as Lisp code.
Call `eval-region' and kill mark. Move back to the beginning of the region."
  (interactive "r")
  (eval-region beg end)
  (setq deactivate-mark t)
  (goto-char beg))

(defun me/browse-url-and-kill-mark (url &rest args)
  "Ask a WWW browser to load URL.
Call `browse-url' and kill mark."
  (interactive (browse-url-interactive-arg "URL: "))
  (apply #'browse-url url args)
  (setq deactivate-mark t))

(defun me/indent-rigidly-left-and-keep-mark (beg end)
  "Indent all lines between BEG and END leftward by one space.
Call `indent-rigidly-left' and keep mark."
  (interactive "r")
  (indent-rigidly-left beg end)
  (setq deactivate-mark nil))

(defun me/indent-rigidly-right-and-keep-mark (beg end)
  "Indent all lines between BEG and END rightward by one space.
Call `indent-rigidly-right' and keep mark."
  (interactive "r")
  (indent-rigidly-right beg end)
  (setq deactivate-mark nil))

(defun me/sort-words (reverse beg end)
  "Sort words in region alphabetically, in REVERSE if negative.
Prefixed with negative \\[universal-argument], sorts in reverse.

The variable `sort-fold-case' determines whether alphabetic case
affects the sort order.

See `sort-regexp-fields'."
  (interactive "*P\nr")
  (sort-regexp-fields reverse "\\w+" "\\&" beg end))
#+END_SRC

Work on lines.

- *TODO*: Handle regions.

#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "<M-S-down>") 'me/duplicate-line-down)
(global-set-key (kbd "<M-S-up>") 'me/duplicate-line-up)
(global-set-key (kbd "<M-down>") 'me/swap-line-down)
(global-set-key (kbd "<M-up>") 'me/swap-line-up)

(defun me/duplicate-line-down ()
  "Duplicate downward the line under point."
  (interactive)
  (kill-whole-line 0)
  (yank)
  (newline)
  (yank)
  (move-beginning-of-line 1))

(defun me/duplicate-line-up ()
  "Duplicate upward the line under point."
  (interactive)
  (kill-whole-line 0)
  (yank)
  (move-beginning-of-line 1)
  (yank)
  (newline)
  (move-beginning-of-line 0))

(defun me/swap-line-down ()
  "Move down the line under point."
  (interactive)
  (forward-line 1)
  (transpose-lines 1)
  (forward-line -1)
  (indent-according-to-mode)
  (delete-trailing-whitespace))

(defun me/swap-line-up ()
  "Move up the line under point."
  (interactive)
  (transpose-lines 1)
  (forward-line -2)
  (indent-according-to-mode)
  (delete-trailing-whitespace))
#+END_SRC

** 17. Projectile

Projectile brings project-level facilities to Emacs such as grep, find and replace.

#+BEGIN_QUOTE
Projectile is a project interaction library for Emacs. Its goal is to provide a nice
set of features operating on a project level without introducing external
dependencies (when feasible). For instance - finding project files has a portable
implementation written in pure Emacs Lisp without the use of GNU find (but for
performance sake an indexing mechanism backed by external commands exists as well).

[[https://github.com/bbatsov/projectile][Bozhidar Batsov]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package projectile
  :init
  (setq-default projectile-keymap-prefix (kbd "C-c C-p"))
  (projectile-global-mode)
  :preface
  (defun me/projectile-project-name (orig-fun &rest args)
    "Prefer `me/project-name' over default Projectile project string."
    (or me/project-name (apply orig-fun args)))
  :config
  (setq-default
   projectile-completion-system 'helm
   projectile-enable-caching t
   projectile-mode-line '(:eval (projectile-project-name)))
  (advice-add 'projectile-project-name :around #'me/projectile-project-name))
#+END_SRC

** 18. Quality of Life

Colorize colors as text with their value.

#+BEGIN_SRC emacs-lisp
(use-package rainbow-mode
  :init (add-hook 'prog-mode-hook 'rainbow-mode)
  :config (setq-default rainbow-x-colors-major-mode-list '()))
#+END_SRC

Augment Emmacs' package menu.

#+BEGIN_QUOTE
Project for modernizing Emacs' Package Menu. With improved appearance, mode-line
information. Github integration, customizability, asynchronous upgrading, and more.

[[https://github.com/Malabarba/paradox][Artur Malabarba]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package paradox
  :config
  (setq-default
   paradox-column-width-package 27
   paradox-column-width-version 13
   paradox-execute-asynchronously t
   paradox-hide-wiki-packages t)
  (set-face-attribute 'paradox-homepage-button-face nil :italic nil)
  (remove-hook 'paradox--report-buffer-print 'paradox-after-execute-functions))
#+END_SRC

** 19. Slack

Slack integration.

- *TODO*: Register teams in =.secret.el=.
- *TODO*: Create a pull request over https://github.com/yuya373/emacs-slack.

#+BEGIN_SRC emacs-lisp
(use-package slack
  :commands (slack-start)
  :config
  (setq-default
   slack-buffer-function 'switch-to-buffer
   slack-prefer-current-team t)
  (slack-register-team
   :name "FiftyFor"
   :default t
   :client-id me/fiftyfor-client-id
   :client-secret me/fiftyfor-client-secret
   :token me/fiftyfor-token
   :subscribed-channels '(dev general))
  (set-face-attribute 'slack-message-output-header nil
                      :foreground zenburn/orange :underline nil)
  (set-face-attribute 'slack-message-output-reaction nil
                      :background zenburn/bg+2 :overline nil)
  (set-face-attribute 'slack-message-output-text nil :height 1))
#+END_SRC

** 20. Whitespaces

Highlight space-like characters, eg. trailing spaces, tabs, empty lines.

- *FIXME*: Force =whitespace-mode= in Org source buffers.

#+BEGIN_SRC emacs-lisp
(use-package whitespace
  :init (global-whitespace-mode 1)
  :config
  (setq-default whitespace-style '(face empty tab trailing))
  (set-face-attribute 'whitespace-empty nil :background zenburn/red-1)
  (set-face-attribute 'whitespace-tab nil :background zenburn/red-1)
  (set-face-attribute 'whitespace-trailing nil :background zenburn/red-1))
#+END_SRC

** 21. Web Browsing

EWW stands for Emacs Web Wowser. Yeah that's right... Emacs is also a Web
browser, text-based that is.

- *NOTE*: Why can't I use =setq-default= to customize =eww-header-line-format=?

#+BEGIN_SRC emacs-lisp
(use-package eww
  :ensure nil
  :delight eww-mode "Emacs Web Wowser"
  :config (setq eww-header-line-format " %t: %u"))
#+END_SRC

** 22. Windows

Adjust the size of every windows and focus the active one. It uses the mathematical
golden ratio somewhere in its formulas.

- *TODO*: Find a way to automatically adjust =golden-ratio-adjust-factor= depending
          on the current display.
- *TODO*: Check out https://github.com/mina86/auto-dim-other-buffers.el.
- *TODO*: Make a hydra.

#+BEGIN_SRC emacs-lisp
(use-package golden-ratio

  :preface
  (defconst me/golden-ratio-adjust-factor-1  .8    "A factor for 16/10: bi-split.")
  (defconst me/golden-ratio-adjust-factor-2  .805  "A factor for 16/9: bi-split.")
  (defconst me/golden-ratio-adjust-factor-3  .53   "A factor for 16/9: tri-split.")
  (defun me/golden-ratio-adjust-1 ()
    (interactive)
    (golden-ratio-adjust me/golden-ratio-adjust-factor-1))
  (defun me/golden-ratio-adjust-2 ()
    (interactive)
    (golden-ratio-adjust me/golden-ratio-adjust-factor-2))
  (defun me/golden-ratio-adjust-3 ()
    (interactive)
    (golden-ratio-adjust me/golden-ratio-adjust-factor-3))
  (defun me/ediff-comparison-buffer-p ()
    (if (boundp 'ediff-this-buffer-ediff-sessions)
        (progn
          (balance-windows)
          ediff-this-buffer-ediff-sessions)))

  :bind
  (("<f6>" . me/golden-ratio-adjust-1)
   ("<f7>" . me/golden-ratio-adjust-2)
   ("<f8>" . me/golden-ratio-adjust-3))

  :init (golden-ratio-mode 1)

  :config
  (setq-default golden-ratio-adjust-factor me/golden-ratio-adjust-factor-2)
  (add-to-list 'golden-ratio-exclude-modes "ediff-mode")
  (add-to-list 'golden-ratio-inhibit-functions 'me/ediff-comparison-buffer-p))
#+END_SRC

Allow undo's and redo's with window configurations.

#+BEGIN_QUOTE
Winner mode is a global minor mode that records the changes in the window
configuration (i.e. how the frames are partitioned into windows) so that the changes
can be "undone" using the command =winner-undo=.  By default this one is bound to the
key sequence ctrl-c left.  If you change your mind (while undoing), you can press
ctrl-c right (calling =winner-redo=).

[[https://github.com/emacs-mirror/emacs/blob/master/lisp/winner.el][Ivar Rummelhoff]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package winner
  :ensure nil
  :if (fboundp 'winner-mode)
  :init (winner-mode 1))
#+END_SRC

-----

*[[#dotemacs][Back to top]]*
